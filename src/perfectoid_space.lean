/-
Perfectoid Spaces

by Kevin Buzzard, Johan Commelin, and Patrick Massot

Definitions in this file follow Scholze's paper: √âtale cohomology of diamonds,
specifically Definition 3.1 and 3.19
-/

-- We import definitions of adic_space, preadic_space, Huber_pair, etc
import Frobenius
import adic_space
import Tate_ring
import power_bounded

section
-- notation for the power bounded subring
local postfix `·µí` : 66 := power_bounded_subring

open nat power_bounded_subring topological_space function

-- We fix a prime number p
parameter (p : primes)

/-- A perfectoid ring is a Huber ring that is complete, uniform,
that has a pseudo-uniformizer whose p-th power divides p in the power bounded subring,
and such that Frobenius is a surjection on the reduction modulo p.-/
structure perfectoid_ring (R : Type) [Huber_ring R] extends Tate_ring R : Prop :=
(complete  : is_complete_hausdorff R)
(uniform   : is_uniform R)
(ramified  : ‚àÉ œñ : pseudo_uniformizer R, œñ^p ‚à£ p in R·µí)
(Frobenius : surjective (Frob R·µí‚àïp))

/-
CLVRS ("complete locally valued ringed space") is a category
whose objects are topological spaces with a sheaf of complete topological rings
and an equivalence class of valuation on each stalk, whose support is the unique
maximal ideal of the stalk; in Wedhorn's notes this category is called ùí±.
A perfectoid space is an object of CLVRS which is locally isomorphic to Spa(A) with
A a perfectoid ring. Note however that CLVRS is a full subcategory of the category
`PreValuedRingedSpace` of topological spaces equipped with a presheaf of topological
rings and a valuation on each stalk, so the isomorphism can be checked in
PreValuedRingedSpace instead, which is what we do.
-/

/-- Condition for an object of CLVRS to be perfectoid: every point should have an open
neighbourhood isomorphic to Spa(A) for some perfectoid ring A.-/
def is_perfectoid (X : CLVRS) : Prop :=
‚àÄ x : X, ‚àÉ (U : opens X) (A : Huber_pair) [perfectoid_ring A],
  (x ‚àà U) ‚àß (Spa A ‚âä U)

/-- The category of perfectoid spaces.-/
def PerfectoidSpace := {X : CLVRS // is_perfectoid X}

end




